// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel init
#pragma kernel emit
#pragma kernel update

#include "UnityCG.cginc"
#include "ParticleCommon.cginc"
#include "Assets/CGINC/SimplexNoise3D.cginc"
// Create a RenderTexture with enableRandomWrite flag and set it
// with cs.SetTexture
RWTexture2D<float4> Result;
RWStructuredBuffer<ParticleData> _Particles;

AppendStructuredBuffer<uint> _Dead;
ConsumeStructuredBuffer<uint> _Pool;
AppendStructuredBuffer<uint> _Active;

// Prop for emit
float3 _PosEmit;
uint _NumEmit;

//prop for Update
float _DT;
float _LifeTime;
float _Gravity;

[numthreads(16,1,1)]
void init (uint3 id : SV_DispatchThreadID)
{
	uint idx = id.x;

	_Particles[idx].isActive = false;
	_Dead.Append(idx);
}

[numthreads(16,1,1)]
void emit (uint3 id : SV_DispatchThreadID)
{
	uint idx = id.x;
	if(idx < _NumEmit){
		idx = _Pool.Consume();
		ParticleData p = _Particles[idx];
		p.isActive = true;
		p.position = _PosEmit;
		float r = 3.1415/_NumEmit;
		p.velocity = float3(sin(id.x*r),cos(id.x*r),sin(id.x*0.01));
		p.velocity = normalize(p.velocity) * 10.0 * frac(id.x*0.01);
		p.color = lerp(float4(0,1,0,1),float4(1,0,0,1),frac(id.x*0.01));
		p.duration = _LifeTime;
		p.size = .1;

		_Particles[idx] = p;
	}
}

float3 divergenceFreeNoise(float3 pos){
	float3 grad0 = snoise_grad(pos);
	float3 grad1 = snoise_grad(pos + 5.33);
	return cross(grad0, grad1);
}

[numthreads(16,1,1)]
void update (uint3 id : SV_DispatchThreadID)
{
	uint idx = id.x;
	ParticleData p = _Particles[idx];
	if(p.isActive){
		p.velocity.y -= _Gravity * _DT;
		p.velocity += divergenceFreeNoise(p.position*0.1) * _DT * 5.;
		p.velocity -= p.position * _DT;
		float speed = length(p.velocity);
		p.velocity *= 1.0 - speed*speed * 0.05 * _DT;

		p.position += p.velocity * _DT;
		p.duration -= _DT;
		if(p.duration < 0){
			p.isActive = false;
			_Dead.Append(idx);
		}
		else
			_Active.Append(idx);
		_Particles[idx] = p;
	}
}